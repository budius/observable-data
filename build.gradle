// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
	ext.kotlin_version = '1.3.50'
	ext.detekt_version = '1.0.0-RC14'

	repositories {
		google()
		jcenter()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:3.5.2'
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
		classpath 'com.vanniktech:gradle-maven-publish-plugin:0.8.0'
		classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
	}
}

allprojects {
	repositories {
		jcenter()
		google()
	}
}

task clean(type: Delete) {
	delete rootProject.buildDir
}

apply from: 'https://raw.githubusercontent.com/sensorberg-dev/gradle-scripts/master/apply-code-style-to-project.gradle'
apply from: './detekt.gradle'

subprojects {
	afterEvaluate { project ->

		def taskString = 'clean detekt testDebugUnitTest lintDebug assembleDebug'
		def taskList = taskString.split(' ')

		task runAll() {
			group = 'verification'
			description = "Same as: $taskString"
		}

		project.tasks.whenObjectAdded { addedTask ->
			if (taskList.contains(addedTask.name)) {
				runAll.dependsOn addedTask.name
			}
		}
	}
}